apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystoreProperties = new Properties()
keystoreProperties.load(rootProject.file("keystore.properties").newDataInputStream())
def localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())

android {
    signingConfigs {
        zhet {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "text.zhet"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 67
        versionName "5.4"
        multiDexEnabled true
        buildConfigField "String", "KEY", localProperties["key"]
    }
    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.zhet
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Textpert.apk"
                }
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/DEPENDENCIES'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.firebase:firebase-core:18.0.2'
    implementation 'com.google.firebase:firebase-ml-vision:19.0.3'
    implementation 'com.google.cloud:google-cloud-translate:1.95.9'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.gms:play-services-ads:19.7.0'
}
apply plugin: 'com.google.gms.google-services'

